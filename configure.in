dnl Process this file with autoconf to produce a configure script.
AC_INIT(lib/dskopen.c)
AC_CONFIG_AUX_DIR(config)
AC_CANONICAL_SYSTEM
VERSION=0.8.2
UPDATED='April 6, 2002'
AM_INIT_AUTOMAKE(libdsk, $VERSION)
AM_CONFIG_HEADER(config.h)

dnl Support for the Java Native Interface bindings...
AC_ARG_WITH(jni,
[  --with-jni              Include Java Native Interface bindings [default=no]],
	      , with_jni=no)
dnl Enable / disable machine-specific "floppy" driver
AC_ARG_ENABLE(floppy,
[  --enable-floppy         Enable host floppy driver [default=yes]],
              , enable_floppy=yes)
AC_JAVA_OPTIONS
if test x$enable_floppy = xyes; then
    :
else
    CFLAGS="$CFLAGS -DDISABLE_FLOPPY"
fi


dnl Checks for programs.
AC_PROG_CC
AC_PROG_INSTALL
dnl Obsoleted by LIBTOOL AC_PROG_RANLIB
AC_PROG_MAKE_SET
AM_PROG_LIBTOOL

dnl GCC extra options
if test "$ac_cv_prog_gcc" = "yes"; then
  CFLAGS="-Wall $CFLAGS"
fi

dnl Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS(errno.h sys/ioctl.h sys/stat.h unistd.h fcntl.h)
AC_CHECK_HEADERS(linux/fd.h linux/fdreg.h windows.h winioctl.h)

dnl Checks for functions
AC_CHECK_FUNCS(strcmpi stricmp strcasecmp, break)

dnl Checks for Java bits
if test x$with_jni = xyes; then
	JAVA=java
	JAVAC=javac
	AC_CHECK_CLASSPATH
	AC_PROG_JAVAC
	AC_PROG_JAVA
	AC_PROG_JAVAH
	AC_PROG_JAVADOC
	AC_PROG_JAR
	AC_JNI_INCLUDE_DIR
     for JNI_INCLUDE_DIR in $JNI_INCLUDE_DIRS
     do
             CPPFLAGS="$CPPFLAGS -I$JNI_INCLUDE_DIR"
     done
	AC_CHECK_HEADERS(jni.h)
	LIBDSKJAR=libdsk.jar
	TOOLCLASSES="DskFormat.class DskID.class DskTrans.class" 
else
	LIBDSKJAR=
	TOOLCLASSES=
	JAVAH=true 
	JAVAC=true 
	JAVA=true
	JAVADOC=true
	JAR=true
fi

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_TYPE_SIZE_T
AC_EXEEXT
AC_OBJEXT

dnl Checks for library functions.
AC_PROG_GCC_TRADITIONAL
AC_HEADER_MAJOR
AC_FUNC_MEMCMP
AC_CHECK_FUNCS(strerror)

AC_SUBST(LIBDSKJAR)
AC_SUBST(TOOLCLASSES)
AC_SUBST(CLASSPATH)
AC_OUTPUT(Makefile include/Makefile lib/Makefile tools/Makefile man/Makefile)
